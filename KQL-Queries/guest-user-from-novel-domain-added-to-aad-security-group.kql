//Repo - T1078.004 Guest user added to AAD Security group

/// KQL to detect if any user from novel domain added to AAD group///
let novelDomains = dynamic([
    "gmail.com", "outlook.com", "hotmail.com", "live.com", "yahoo.com",
    "icloud.com", "aol.com", "mail.com", "protonmail.com", "zoho.com",
    "gmx.com", "yandex.com", "tuta.com", "fastmail.com", "msn.com",
    "ymail.com", "qq.com", "163.com", "126.com", "mail.ru",
    "web.de", "t-online.de", "naver.com", "rediffmail.com",
    "libero.it", "freenet.de", "comcast.net", "xtra.co.nz"
]);
let InvitedUsers = AuditLogs
| where ActivityDisplayName == "Invite external user"
| extend InvitedUser = tostring(AdditionalDetails[5].value)
| extend InvitedUserDomain = tostring(split(InvitedUser, "@")[1])
| where InvitedUserDomain in~ (novelDomains)
| extend InvitationMethod = tostring(AdditionalDetails[6].value)
| extend InvitingUser = tostring(InitiatedBy.user.userPrincipalName)
| project InvitedUser, InvitationTime = TimeGenerated, InvitationMethod, InvitingUser;
let GroupAdditions = AuditLogs
| where OperationName in~ ("Add member to group", "Add member to role", "Add eligible member to role", "Add active member to role")
| extend TargetUser = tostring(TargetResources[0].userPrincipalName)
| mv-expand ModifiedProps = TargetResources[0].modifiedProperties
| extend PropName = tostring(ModifiedProps.displayName), NewValue = tostring(ModifiedProps.newValue)
| where PropName == "Group.DisplayName"
| extend GroupName = trim(@'"', NewValue);
GroupAdditions
| join kind=inner (
    InvitedUsers
) on $left.TargetUser == $right.InvitedUser
| summarize LatestGroupAddTime = max(TimeGenerated), any(OperationName), any(InvitationMethod), any(InvitingUser)
    by InvitedUser=TargetUser, GroupName
| project LatestGroupAddTime, InvitedUser, GroupName, OperationName = any_OperationName, InvitationMethod = any_InvitationMethod, InvitingUser = any_InvitingUser
| order by LatestGroupAddTime desc